# Load the trajectory
trajin [FOLDER]/mergedResult.dcd 1 last 1

# Cluster Analysis
##################
cluster hieragglo \
    epsilon 3.0 \
    averagelinkage \
    rms :1-[LASTRESI] @CA,C,N \
    out [FOLDER]/analysis/data/cluster_hier_out \
    gracecolor \
    summary [FOLDER]/analysis/data/cluster_hier_summary.dat \
    info [FOLDER]/analysis/data/cluster_hier_info \
    repout [FOLDER]/analysis/data/cluster_hier_centroid repfmt pdb sieve 25 

# Dihedral angles
#################
[DIHEDRALS]

# Hydrogen Bon analysis
#######################

# Run the analysis
hbond :1-[LASTID] out [FOLDER]/analysis/data/hbond.out avgout [FOLDER]/analysis/data/hbond.avg


# Run PCA analysis
##################
# See paper: 
# "Evaluation of Enhanced Sampling Provided by Accelerated Molecular 
#  Dynamics with Hamiltonian Replica Exchange Methods‚Äù 
# by DR Roe, C Bergonzo, and TE Cheatham, III

# Average the trajectory
rms first :1-[LASTRESI]&!@H=
average [FOLDER]/analysis/data/averageStructure.rst restart

# Save coordinates in memory
createcrd crd1

# Run for further processing
run

# Create reference
reference [FOLDER]/analysis/data/averageStructure.rst.1 [avg]

# RMS-fit coordinates in crd1 to average
crdaction crd1 rms ref [avg] mass :1-[LASTRESI]&!@H= out [FOLDER]/analysis/data/RMSD.out

# Create covariance matrix for all heavy atoms
crdaction crd1 matrix covar name matrixdat :1-[LASTRESI]&!@H= out [FOLDER]/analysis/data/covmat-ca.dat

# Diagonalize coordinate covariance matrix for eigenmodes 
analyze matrix matrixdat out [FOLDER]/analysis/data/evecs-ca.dat vecs 100

# Run the matrix stuff. Otherwise they will not be initialized for the upcoming projection
run 

# Project the modes
crdaction crd1 projection modes [FOLDER]/analysis/data/evecs-ca.dat out [FOLDER]/analysis/data/pca12-ca beg 1 end 5 :1-[LASTRESI]&!@H=

# Run analysis
go